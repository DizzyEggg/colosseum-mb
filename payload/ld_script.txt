gNumMusicPlayers = 1;
gMaxLines = 0;

GPIOPortDirection = 0x080000C6;

SECTIONS {
    . = 0x02000000;
    .text :
    ALIGN(4)
    {
        asm/crt0.o(.text);
        src/all.o(.text);
        asm/all.o(.text);
    } =0

    lib_text :
    ALIGN(4)
    {
        asm/m4a_1.o(.text);
        /* asm/m4a.o(.text); */
        src/m4a.o(.text);
        src/main.o(.text);
        asm/main.o(.text);
        src/main.o(.text.020092C0);
        asm/main.o(.text.020092F0);
        src/agb_flash.o(.text);
        src/agb_flash_1m.o(.text);
        src/agb_flash_mx.o(.text);
        src/agb_flash_le.o(.text);
        src/unk_200E344.o(.text);
        src/siirtc.o(.text);
        asm/libagbsyscall.o(.text);
        *libgcc.a:_call_via_rX.o(.text);
        *libgcc.a:_divsi3.o(.text);
        *libgcc.a:_dvmd_tls.o(.text);
        *libgcc.a:_modsi3.o(.text);
        *libgcc.a:_umodsi3.o(.text);
        *libc.a:memcpy.o(.text);
    } =0
    .rodata :
    ALIGN(4)
    {
        data/all.o(.rodata);
    } =0

    m4a_rodata :
    ALIGN(4)
    {
        src/m4a_tables.o(.rodata);
        data/sound_data.o(.rodata);
        sound/songs/midi/se_select.o(.rodata);
        sound/songs/midi/se_failure.o(.rodata);
    } =0

    lib_rodata :
    ALIGN(4)
    {
        data/main.o(.rodata);
        src/agb_flash.o(.rodata);
        src/agb_flash_1m.o(.rodata);
        src/agb_flash_mx.o(.rodata);
        src/agb_flash_le.o(.rodata);
        src/unk_200E344.o(.rodata);
        src/siirtc.o(.rodata);
	} =0

	.data :
	ALIGN(4)
	{
        data/all.o(.data);
	} =0

	lib_data :
	ALIGN(4)
	{
        data/main.o(.data);
    } =0
    ewram_start = .;
    wram (NOLOAD) :
    ALIGN(4)
    {
    INCLUDE sym_bss.ld;
    src/m4a.o(.bss.code);
    INCLUDE sym_common.ld;
    INCLUDE sym_ewram.ld;
    }
    ewram_end = .;
    wram_extra (NOLOAD) :
    ALIGN(4)
    {
    gUnknown_0202526C = .;
    . += 0x400;
    }
    . = 0x02040000; /* Limit EWRAM size */

    . = 0x03000000;
    gpu_buffer (NOLOAD) :
    ALIGN(4)
    {
    gBgTilemapBuffers = .;
    . += 0x4000;
    }
    . = 0x03008000; /* Limit IWRAM size */
    /DISCARD/ :
    {
        *(*);
    }
}
